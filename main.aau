import win.ui;
import win.ui.atom;
import fsys;
import fsys.ini;
import fsys.dlg;
import inet;
import inet.whttp;
loadcode("\res\b32decoder.aau")() //BASE32解码
loadcode("\res\logger.aau")() //日志记录函数
loadcode("\res\get_flow.aau")() //流水下载函数
loadcode("\res\flow_handle.aau")() //流水解码函数
/*DSG{{*/
var winform = ..win.form( text="腾讯罗盘";parent=...;bottom=287;max=false;right=423;border="dialog frame" )
winform.add( 
cb_timeStart={ bottom=76;color=0;right=152;left=72;
items={ "00:00";"02:00";"04:00";"06:00";"08:00";"10:00";"12:00";"14:00";"16:00";"18:00";"20:00";"22:00" };font=LOGFONT( name='宋体';h=-14 );z=11;top=56;mode="dropdownlist";edge=1;cls="combobox" };
chk_debug={ bottom=144;color=0;text="DEBUG_MODE";left=296;top=120;font=LOGFONT( name='黑体';h=-14 );z=24;right=392;cls="checkbox" };
btn_path={ bottom=192;color=0;right=392;left=296;top=168;font=LOGFONT( name='宋体';h=-14 );z=21;text="选择文件";cls="button" };
gb_split={ bottom=160;color=0;right=392;left=32;top=144;font=LOGFONT( name='宋体';h=-14 );z=25;edge=1;cls="groupbox" };
cb_timeEnd={ bottom=76;color=0;right=288;left=208;
items={ "01:59";"03:59";"05:59";"07:59";"09:59";"11:59";"13:59";"15:59";"17:59";"19:59";"21:59";"23:59" };font=LOGFONT( name='宋体';h=-14 );z=12;top=56;mode="dropdownlist";edge=1;cls="combobox" };
st_column={ bottom=232;align="center";right=112;left=24;top=192;font=LOGFONT( name='宋体';h=-14 );center=1;z=8;transparent=1;color=0;text="流水显示列";cls="static" };
st_timeEnd={ bottom=88;align="center";right=208;left=160;top=48;font=LOGFONT( name='宋体';h=-14 );center=1;z=4;transparent=1;color=0;text="结束";cls="static" };
ed_rowFilter={ bottom=256;color=0;right=288;left=112;top=232;font=LOGFONT( name='宋体';h=-14 );z=16;align="center";edge=1;cls="edit" };
picturebox={ bottom=112;color=0;right=392;left=296;top=56;font=LOGFONT( name='宋体';h=-14 );z=23;notify=1;image=$"\mix\logo.jpg";cls="picturebox" };
gb_frame={ bottom=272;color=0;right=408;left=16;top=0;font=LOGFONT( name='宋体';h=-14 );z=1;edge=1;cls="groupbox" };
ed_path={ bottom=192;color=0;right=288;left=32;top=168;font=LOGFONT( name='宋体';h=-14 );z=13;align="center";edge=1;cls="edit" };
st_user={ bottom=120;align="center";right=72;left=24;top=80;font=LOGFONT( name='宋体';h=-14 );center=1;z=6;transparent=1;color=0;text="用户";cls="static" };
st_date={ bottom=144;align="center";right=72;left=24;top=120;font=LOGFONT( name='宋体';h=-14 );center=1;z=7;transparent=1;color=0;text="日期";cls="static" };
btn_load={ bottom=144;color=0;right=288;left=218;top=88;font=LOGFONT( name='宋体';h=-14 );z=19;text="加载到
缓冲区";cls="button" };
cb_locate={ disabled=1;bottom=44;color=0;right=152;left=72;
items={  };font=LOGFONT( name='宋体';h=-14 );z=9;top=24;mode="dropdownlist";edge=1;cls="combobox" };
st_domain={ bottom=56;align="center";right=208;left=160;top=16;font=LOGFONT( name='宋体';h=-14 );center=1;z=3;transparent=1;color=0;text="平台";cls="static" };
st_timeStart={ bottom=88;align="center";right=72;left=24;top=48;font=LOGFONT( name='宋体';h=-14 );center=1;z=5;transparent=1;color=0;text="开始";cls="static" };
ed_colFilter={ bottom=224;color=0;right=288;left=112;top=200;font=LOGFONT( name='宋体';h=-14 );z=15;align="center";edge=1;cls="edit" };
cb_rowFilter={ bottom=256;color=0;right=104;left=32;
items={ "操作号";"关键字" };font=LOGFONT( name='宋体';h=-14 );z=26;top=232;mode="dropdownlist";edge=1;cls="combobox" };
btn_login={ bottom=48;color=0;right=392;left=296;top=24;font=LOGFONT( name='宋体';h=-14 );z=20;text="罗盘登录";cls="button" };
datetimepick={ bottom=144;color=0;right=168;left=72;top=120;font=LOGFONT( name='宋体';h=-14 );z=17;edge=1;cls="datetimepick" };
cb_domain={ bottom=44;color=0;right=288;left=208;
items={  };font=LOGFONT( name='宋体';h=-14 );z=10;top=24;mode="dropdownlist";edge=1;cls="combobox" };
ed_user={ bottom=112;color=0;right=208;left=72;top=88;font=LOGFONT( name='宋体';h=-14 );z=14;align="center";edge=1;cls="edit" };
st_locate={ bottom=56;align="center";right=72;left=24;top=16;font=LOGFONT( name='宋体';h=-14 );center=1;z=2;transparent=1;color=0;text="地区";cls="static" };
btn_save={ bottom=144;color=0;right=216;left=176;top=120;font=LOGFONT( name='宋体';h=-14 );z=18;text="SAVE";cls="button" };
btn_decode={ bottom=256;color=0;right=392;left=296;top=200;font=LOGFONT( name='宋体';h=-16 );z=22;text="流水解码";cls="button" }
)
/*}}*/

//双击打开网页罗盘
winform.picturebox.wndproc = function(hwnd,message,wParam,lParam){
	select(message) {
		case 0x203/*_WM_LBUTTONDBLCLK*/ {
			import process;
			process.execute("http://compass.qq.com/dlogs?appid=" ++ iniFile.read("rabbet","appid","0"))
		}
	}
	//无返回值则继续调用默认回调函数
}


//*点击『流水解码』按钮{{*/
winform.btn_decode.oncommand = function(id,event){
	winform.btn_decode.disabled = true //避免重复点击
	
	var logToFile = "" //声明保存解码文件的路径
	if( #string.trim(winform.ed_path.text) ){
		if( (winform.ed_path.disabled == true) and (string.startWith(winform.ed_path.text,"<缓冲区>:")) ){
			/*{ 流水内容已加载到global.flowContentRaw全局变量 }*/
			logToFile = fsys.getTempDir() + "FlowBuffer.ada"
		}
		elseif( (winform.ed_path.disabled == false) and io.exist(winform.ed_path.text) ){
			var flowPath = io.splitpath(winform.ed_path.text)
			logToFile = flowPath.dir + flowPath.file + ".txt" //保存到同级目录下的txt文件中
			global.flowContentRaw = string.load(winform.ed_path.text) //读取硬盘中的流水文件内容
		}
		else {
			winform.btn_decode.disabled = false //激活按钮
			win.msgbox("没有流水文件可以解析！")
			return "ERR_NO_FLOW_FILE";  //跳过当前函数剩余代码
		}
	}
	
	//调用『流水解码』函数
	if not( global.flowContentRaw ){
		winform.btn_decode.disabled = false //激活按钮
		win.msgbox("流水为空，无法解析！")
		return "ERR_NO_FLOW_DATA";  //跳过当前函数剩余代码
	}
	global.flowResult = flowHandle({
			colFilter = string.trim(winform.ed_colFilter.text); //过滤指定列
			rowFilter = string.trim(winform.ed_rowFilter.text); //过滤指定行
			rowFilterType = winform.cb_rowFilter.selText; //过滤指定行的方式
			flowContentRaw = global.flowContentRaw; //原始流水内容
			userNumber = string.trim(tostring(winform.ed_user.text)) //用户ID
	})
	
	if( log2log(global.flowResult,logToFile) ){ //保存成功后直接打开
		process.execute(logToFile)
	}
	
	winform.btn_decode.disabled = false //激活按钮
}


//点击『加载流水』按钮
winform.btn_load.oncommand = function(id,event){
	winform.btn_load.disabled = true; winform.btn_load.text = '正在\n加载' //避免重复点击

	var errorBox = ""
	if not( tonumber(winform.ed_user.text) )
			{ errorBox += '用户UID不正确！\n' }
	if( tonumber(winform.cb_timeStart.selText) > tonumber(winform.cb_timeEnd.selText) )
			{ errorBox += '开始时间大于结束时间！\n' }
	
	if( #errorBox ){
		win.msgbox(errorBox)
	}
	else{
		var year_month_day = winform.datetimepick.time
		year_month_day.format="%Y-%m-%d";

		returnStat, global.flowContentRaw = get_flow({
				biscuit = global.biscuit;
				appid = iniFile.read("rabbet","appid","0");
				locate = iniFile.read("locate",winform.cb_locate.selText);
				domain = iniFile.read("domain",winform.cb_domain.selText);
				baseday = tostring(year_month_day);
				timeStart = string.replace( winform.cb_timeStart.selText + ":00","\:","%3A" );
				timeEnd = string.replace( winform.cb_timeEnd.selText + ":00","\:","%3A" );
				user = tostring( winform.ed_user.text );
				debug = winform.chk_debug.checked
		})

		if not(returnStat){
			qq_login() //需要登录获取加载流水的权限
		}
		else {
			winform.ed_path.text = "<缓冲区>:" + tostring(year_month_day) + "[" + winform.ed_user.text + "]"
			winform.ed_path.disabled = true
		};
	}
	
	winform.btn_load.disabled = false; winform.btn_load.text = '加载到\n缓冲区' //启用按钮
}


//登录按钮
winform.btn_login.oncommand = function(id,event){
	qq_login()
}


//打开硬盘上的流水文件
winform.btn_path.oncommand = function(id,event){
	winform.ed_path.disabled = false
	winform.ed_path.text = fsys.dlg.open("所有文件(*.*)|*.*|")
}


//保存流水到硬盘文件
winform.btn_save.oncommand = function(id,event){
	if(win.msgboxTest('点"确定"保存最原始的数据\n点"取消"保存解析后的流水')){
		if( (winform.ed_path.disabled == true) and (string.startWith(winform.ed_path.text,"<缓冲区>:")) ){
			var save_path = fsys.dlg.save("默认文件类型(csv)|*.csv|所有文件类型(*.*)|*.*|","保存缓冲区数据到文件")
			if( #save_path ){string.save( save_path,global.flowContentRaw )}
		}
		else {winform.msgbox("缓存区中没有数据！")}
	}
	else {
		if( #global.flowResult ){
			var save_path = fsys.dlg.save("默认文件类型(txt)|*.txt|所有文件类型(*.*)|*.*|","保存解码的流水到文件")
			if( #save_path ){string.save( save_path,global.flowResult )}
		}
		else {winform.msgbox("没有被解码的流水！")}
	}
}


//用QQ登录罗盘
qq_login = function(){
	var child,wb = winform.loadForm("\res\qq_login.aau",winform /*指定父窗口*/ );
	child.login(15004102); //认证ID，取自原登录页面
}


//退出程序时记录COOKIES
winform.wndproc = function(hwnd,message,wParam,lParam){
	select(message) {
		case 0x10/*_WM_CLOSE*/ {
			iniFile.write("caches","biscuit",global.biscuit)
		}
	}
}


/*预处理{{*/
iniFile=string.replace(io._exepath,"\.\w*$","\.ini")
if not( io.exist(iniFile) ){//若不存在配置文件则从res中释放
	string.save(iniFile,string.load("\res\config.ini"))
}
iniFile = fsys.ini(iniFile)
//全局性变量
global.biscuit="" //COOKIES
global.flowResult = "" //流水解码结果
global.flowContentRaw = "" //原始流水数据
//请求头
global.biscuit = iniFile.read("caches","biscuit","")
//地区
winform.cb_locate.items=iniFile.readKeys("locate")
winform.cb_locate.selIndex=1
//平台
winform.cb_domain.items=iniFile.readKeys("domain")
winform.cb_domain.selIndex=1
//显示明细
winform.chk_debug.checked=tonumber(iniFile.read("rabbet","debugMode","0"))
//过滤方式、显示列
winform.cb_rowFilter.selIndex=1
winform.ed_colFilter.text=iniFile.read("column","display","")
//时间范围
winform.cb_timeStart.selIndex=1
winform.cb_timeEnd.selIndex = #winform.cb_timeEnd.items
//预加载流水文件
if(_CMDLINE){ winform.ed_path.text=string.replace(_CMDLINE,'"','') }
//标题表明APPID
winform.text = "罗盘【" ++ iniFile.read("rabbet","appid","0") ++ "】"
/*}}*/


//以下是窗口消息循环
winform.show() 
win.loopMessage()
