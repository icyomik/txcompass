import win.ui;
import process;
import fsys;
import fsys.dlg;
loadcode("\res\logger.aau")() //记录日志到文件
loadcode("\res\readflow.aau")() //下载罗盘流水
loadcode("\res\handleflow.aau")() //处理流水内容
/*DSG{{*/
var mainForm = ..win.form( parent=...;bottom=255;max=false;text="腾讯罗盘流水";border="dialog frame";right=431 )
mainForm.add( 
cb_begintime={ bottom=104;color=0;right=120;left=56;top=80;font=LOGFONT( name='黑体';h=-16 );z=10;
items={ [1]="00:00";[2]="02:00";[3]="04:00";[4]="06:00";[5]="08:00";[6]="10:00";[7]="12:00";[8]="14:00";[9]="16:00";[10]="18:00";[11]="20:00";[12]="22:00" };mode="dropdownlist";edge=1;cls="combobox" };
gb_mright={ bottom=152;color=0;right=416;left=128;top=64;font=LOGFONT( name='Cambria' );z=6;cls="groupbox" };
ed_filter={ bottom=224;autohscroll=false;color=0;right=296;left=120;top=200;font=LOGFONT( name='微软雅黑';h=-16 );align="center";z=14;hidesel=1;autovscroll=false;edge=1;cls="edit" };
btn_web={ bottom=56;color=0;right=336;left=304;top=32;font=LOGFONT( name='微软雅黑' );z=26;text="网页";cls="button" };
cb_game={ bottom=56;color=0;right=280;left=176;top=32;font=LOGFONT( name='黑体';h=-16 );z=22;
items={  };mode="dropdownlist";edge=1;cls="combobox" };
dtp_date={ bottom=136;color=0;right=296;left=176;top=112;font=LOGFONT( name='黑体';h=-16 );z=9;edge=1;cls="datetimepick" };
btn_save={ bottom=104;color=0;right=296;left=280;top=80;font=LOGFONT( name='微软雅黑';h=-16 );z=12;text="S";cls="button" };
btn_debug={ bottom=56;color=0;right=368;left=336;top=32;font=LOGFONT( name='微软雅黑' );z=25;text="调试";cls="button" };
cb_plat={ bottom=56;color=0;right=120;left=32;top=32;font=LOGFONT( name='黑体';h=-16 );z=8;
items={  };mode="dropdownlist";edge=1;cls="combobox" };
st_begintime={ bottom=104;color=0;nWrap=1;left=32;align="center";top=80;font=LOGFONT( name='微软雅黑';h=-16 );center=1;transparent=1;right=56;text="始";z=21;cls="static" };
st_outline={ disabled=1;bottom=160;align="center";right=400;left=32;top=144;font=LOGFONT( name='宋体' );center=1;z=15;color=0;border=1;cls="static" };
ed_column={ bottom=192;autohscroll=false;color=0;right=296;left=120;top=168;font=LOGFONT( name='微软雅黑';h=-14 );align="center";z=13;hidesel=1;autovscroll=false;edge=1;cls="edit" };
cb_filter={ bottom=224;color=0;right=112;left=32;top=200;font=LOGFONT( name='黑体';h=-16 );z=17;
items={ [1]="#操作号";[2]="@关键字" };mode="dropdownlist";edge=1;cls="combobox" };
gb_mleft={ bottom=152;color=0;right=128;left=16;top=64;font=LOGFONT( name='Cambria' );z=5;cls="groupbox" };
st_endtime={ bottom=136;color=0;nWrap=1;left=32;align="center";top=112;font=LOGFONT( name='微软雅黑';h=-16 );center=1;transparent=1;right=56;text="终";z=23;cls="static" };
btn_file={ bottom=56;color=0;right=296;left=280;top=32;font=LOGFONT( name='微软雅黑';h=-16 );z=27;text="F";cls="button" };
st_user={ bottom=104;color=0;nWrap=1;left=136;align="center";top=80;font=LOGFONT( name='微软雅黑';h=-16 );center=1;transparent=1;right=176;text="用户";z=18;cls="static" };
gb_head={ bottom=72;color=0;right=416;left=16;top=8;font=LOGFONT( name='Cambria' );z=7;cls="groupbox" };
st_game={ bottom=56;color=0;nWrap=1;left=136;align="center";top=32;font=LOGFONT( name='微软雅黑';h=-16 );center=1;transparent=1;right=176;text="游戏";z=19;cls="static" };
btn_login={ bottom=56;color=0;right=400;left=368;top=32;font=LOGFONT( name='微软雅黑' );z=24;text="登录";cls="button" };
st_date={ bottom=136;color=0;nWrap=1;left=136;align="center";top=112;font=LOGFONT( name='微软雅黑';h=-16 );center=1;transparent=1;right=176;text="日期";z=20;cls="static" };
gb_main={ bottom=240;color=0;right=416;left=16;top=8;font=LOGFONT( name='Cambria' );z=4;cls="groupbox" };
st_column={ bottom=191;color=0;nWrap=1;left=32;align="center";top=168;font=LOGFONT( name='黑体';h=-16 );center=1;transparent=1;right=112;text="数据显示列";z=16;cls="static" };
cb_endtime={ bottom=136;color=0;right=120;left=56;top=112;font=LOGFONT( name='黑体';h=-16 );z=11;
items={ [1]="01:59";[2]="03:59";[3]="05:59";[4]="07:59";[5]="09:59";[6]="11:59";[7]="13:59";[8]="15:59";[9]="17:59";[10]="19:59";[11]="21:59";[12]="23:59" };mode="dropdownlist";edge=1;cls="combobox" };
btn_load={ bottom=136;color=0;right=400;left=304;top=80;font=LOGFONT( name='微软雅黑';h=-19 );z=2;text="加载罗盘";cls="button" };
ed_user={ right=280;num=1;bottom=104;color=0;hidesel=1;left=176;top=80;font=LOGFONT( name='微软雅黑';h=-16 );z=3;align="center";edge=1;cls="edit" };
btn_decode={ bottom=224;color=0;right=400;left=304;top=168;font=LOGFONT( name='微软雅黑';h=-19 );z=1;text="解码流水";cls="button" }
)
/*}}*/

var cfgfolder = string.replace(io._exepath,"\.\w*$","") //建立同名目录
if not( fsys.isDir(cfgfolder) ){ fsys.createDir(cfgfolder) }

/*初始化{{*/
var mixer = loadcode("\res\mixfunc.aau") //返回基础数据

var inifile = mixer("initial_config") //程序配置
var commtbl = mixer("obtain_commtable") //公共表
mixer("generate_column") //生成默认的显示列

global.biscuit = inifile.read("share","biscuit") : "cookie:" //请求头

//默认焦点在用户ID框
mainForm.ed_user.setFocus()
//默认过滤方式
mainForm.cb_filter.selIndex = 1
//所在平台
mainForm.cb_plat.items = inifile.readKeys("domain")
mainForm.cb_plat.selIndex = 1
//应用名称
mainForm.cb_game.items = inifile.readKeys("appset")
mainForm.cb_game.selIndex = 1
//查询时间段（默认全天）
mainForm.cb_begintime.selIndex = 1
mainForm.cb_endtime.selIndex = #mainForm.cb_endtime.items
//程序控件配置（上次退出时的设置）
import config;
mainForm.bindConfig( config.widget,{
	cb_plat = "selIndex";
	cb_game = "selIndex";
	cb_filter = "selIndex";
} );

mainForm.dtp_date.setFormat("yyyy'/'MM'/'dd") //日期格式，防止受系统时间格式影响
/*}}*/

//应用ID被改变
mainForm.cb_game.oncommand = function(id,event){
	if(event == 0x1/*_CBN_SELCHANGE*/){
		resumeInitialAndChange()
	}
}

//打开罗盘网页
mainForm.btn_web.oncommand = function(id,event){
	process.execute("http://compass.qq.com/dlogs?appid=" ++ commtbl.appid)
}

//退出程序时记录COOKIES
mainForm.wndproc = function(hwnd,message,wParam,lParam){ 
	select(message) {
		case 0x10/*_WM_CLOSE*/ {
			inifile.write("share","biscuit",global.biscuit)
		}
	}
}

//QQ登录窗口
qqLogin = function(){
	var qq,wb = mainForm.loadForm("\res\qqlogin.aau", mainForm/*父窗口*/); 
	qq.login(15004102/*,true*/) //认证ID，取自原登录页面
	var qqlogin = qq.doModal(mainForm)
}

//点击「登录」按钮
mainForm.btn_login.oncommand = function(id,event){
	qqLogin()
}

//硬盘上的流水文件路径
mainForm.btn_file.oncommand = function(id,event){
	var select_flow = fsys.dlg.open("所有文件(*.*)|*.*|")
	if(!select_flow){ return } //没有选择文件，返回
	mainForm.st_outline.text = select_flow
	//mainForm.ed_user.text = io.splitpath(select_flow).file
	mainForm.ed_user.text = "[1-9]+"
}

//保存流水到硬盘文件
mainForm.btn_save.oncommand = function(id,event){
	if( string.startWith(mainForm.st_outline.text,"[罗盘]:") ){
		var year_month_day = mainForm.dtp_date.time
		year_month_day.format = "%y%m%d"
		
		var save_path = fsys.dlg.save("默认文件类型(csv)|*.csv|所有文件类型(*.*)|*.*|",
						"保存原始流水到文件",,,,mainForm.ed_user.text ++ "_" ++ tostring(year_month_day))
		
		if( #save_path ){string.save( save_path,commtbl.flow_raw )}
	} else {
		win.msgbox("请先加载罗盘流水！","错误")
	}
}

//点击『加载罗盘』按钮
mainForm.btn_load.oncommand = function(id,event){
	mainForm.btn_load.disabled = true; mainForm.btn_load.text = "加载ING" //避免重复点击


	var err_msgbox = ""
	if(mainForm.ed_user.text != tonumber(mainForm.ed_user.text)){
		err_msgbox += '用户UID仅能为数字！\n'
	}
	if(tonumber(mainForm.cb_begintime.selText) > tonumber(mainForm.cb_endtime.selText)){
		err_msgbox += '开始时间大于结束时间！\n'
	}
	
	if(#err_msgbox){
		win.msgbox(err_msgbox)
	} else {
		var year_month_day = mainForm.dtp_date.time
		year_month_day.format = "%Y-%m-%d"
		
		qqstat, commtbl.flow_raw = readFlow({ //加载流水到内存变量
			biscuit = global.biscuit;
			appid = commtbl.appid; //罗盘应用ID
			domain = inifile.read("domain",mainForm.cb_plat.selText,"1"); //应用所在平台，默认「QQ空间」
			date = tostring(year_month_day); //查询日期
			tm_begin = string.replace(mainForm.cb_begintime.selText ++ ":00", "\:", "%3A"); //开始时间
			tm_end = string.replace(mainForm.cb_endtime.selText ++ ":59", "\:", "%3A"); //结束时间
			user = tostring(mainForm.ed_user.text) //用户
		})
		
		if not(qqstat){
			qqLogin() //需要登录能够加载罗盘权限的QQ
		} else {
			mainForm.st_outline.text = "[罗盘]:" ++ tostring(year_month_day) ++ ":[" ++ mainForm.ed_user.text ++ "]"
		}
	}
	
	mainForm.btn_load.disabled = false; mainForm.btn_load.text = "加载罗盘" //启用按钮
}

//*点击『流水解码』按钮{{*/
mainForm.btn_decode.oncommand = function(id,event){
	mainForm.btn_decode.disabled = true; mainForm.btn_decode.text = "解码ING" //避免重复点击
	
	var flow_output = "" //保存流水解码后的文件路径
	mainForm.st_outline.text = string.trim(mainForm.st_outline.text)
	if(#mainForm.st_outline.text){
		if(string.startWith(mainForm.st_outline.text,"[罗盘]:")){
			flow_output = fsys.getTempDir() ++ "flow." ++ commtbl.appid ++ ".ada"
			/*{ 流水已加载到内存变量中，无需再读取 }*/
		} elseif (io.exist( mainForm.st_outline.text )) {
			flow_output = tostring(mainForm.st_outline.text) ++ ".txt" //保存到同级目录下的txt文件中
			commtbl.flow_raw = string.load(mainForm.st_outline.text) //载入硬盘上的流水文件
		}
	} else {
		mainForm.btn_decode.disabled = false; mainForm.btn_decode.text = "解码流水" //启用按钮
		win.msgbox("没有流水数据，无法解析！","错误")
		return "ERR_NO_FLOW_CONTENT"; //跳过当前函数的剩余代码
	}
	
	commtbl.flow_result = handleFlow({ //解码流水的函数
		flow_raw = commtbl.flow_raw; //原始流水内容
		user = tostring(mainForm.ed_user.text); //用户ID
		column = tostring(mainForm.ed_column.text); //过滤列
		filter = tostring(mainForm.ed_filter.text); //过滤模板
		filter_type = tostring(mainForm.cb_filter.selText); //过滤方式
	})
	
	if( log2file(commtbl.flow_result,flow_output) ){ //保存成功后直接打开
		process.execute(flow_output)
	}
	
	mainForm.btn_decode.disabled = false; mainForm.btn_decode.text = "解码流水" //启用按钮
}

//调试
mainForm.btn_debug.oncommand = function(id,event){
	win.invoke(
		function( ... ){ import console; console.varDump( ... ); console.pause(true); },
		commtbl.appid, global.biscuit, commtbl.flow_raw
	)
}

//标题中显示APPID、默认显示列
resumeInitialAndChange = function(){ //当APPID改变时也会触发这个函数
	mainForm.text = "罗盘【" ++ mainForm.cb_game.selText ++ "】"
	commtbl.appid = tostring( inifile.read("appset",mainForm.cb_game.selText,"19089") )
	mainForm.ed_column.text = inifile.read("column",mainForm.cb_game.selText,"5,9,14,16,18,19,20,21,22,29")
}
resumeInitialAndChange()

//预加载流水文件
if( _CMDLINE ){
	mainForm.st_outline.text = string.replace(_CMDLINE,'"','')
	mainForm.ed_user.text = io.splitpath(mainForm.st_outline.text).file
}

//显示程序界面
mainForm.show() 
win.loopMessage(); 
